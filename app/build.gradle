plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
}

def keyStorePropertiesFileName = "keystore.properties"

android {
    compileSdkVersion 31

    signingConfigs {
        release {
            if (rootProject.file(keyStorePropertiesFileName).exists()) {
                // Load keystore
                def keystorePropertiesFile = rootProject.file(keyStorePropertiesFileName)
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    defaultConfig {
        applicationId "com.etsisi.appquitectura"
        minSdk 21
        targetSdk 31
        versionCode rootProject.ext.appVersionCode
        versionName rootProject.ext.appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            debuggable true
        }
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
    packagingOptions {
        resources.excludes += "DebugProbesKt.bin"
    }
}

dependencies {

    // AndroidX
    implementation rootProject.ext.androidAppCompat
    implementation rootProject.ext.androidKotlinCore
    implementation rootProject.ext.fragmentKtxDependency
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //Coroutines
    implementation rootProject.ext.coroutinesKtxDependency

    //Firebase
    implementation platform(rootProject.ext.BOMdependency)
    implementation rootProject.ext.firebaseAuthDependency
    implementation rootProject.ext.firestoreDependency
    implementation rootProject.ext.dynamicLinksDependency

    //Flexbox
    implementation rootProject.ext.flexboxDependency

    //Google Play Services
    implementation rootProject.ext.googleAuthDependency

    //Glide
    implementation rootProject.ext.glideDependency
    kapt rootProject.ext.glideAnnotation

    //Koin (Dependency injection)
    implementation rootProject.ext.koinDependency

    //Lifecycle - Architecture components
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycleVersion"
    implementation rootProject.ext.liveDataKtxDependency
    implementation rootProject.ext.savedStateKtxDependency
    implementation rootProject.ext.lifecycleKtxDependency
    implementation rootProject.ext.viewModelKtxDependency

    //Lottie
    implementation rootProject.ext.lottieDependency

    //Material
    implementation rootProject.ext.materialDependency

    //JetPack
    implementation rootProject.ext.navigationFragKtxDependency
    implementation rootProject.ext.navigationKtxDependency

    //Progress Button
    implementation rootProject.ext.progressButtonDependency

    // Retrofit
    implementation rootProject.ext.retrofitDependency
    implementation rootProject.ext.retrofitGsonDependency

    //Splash
    implementation rootProject.ext.splashScreenDependency
}